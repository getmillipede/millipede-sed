#!/bin/sh

sed -n '
{
# lets draw milli!
/^END$/ b compute

# reverse mode flag
s/^ -r$/REVERSE/
t copy

# size flag
s/^ -s\([0-9][0-9]\)$/S\1S/
t copy
s/^ -s\([0-9]\)$/S\1S/
t copy

# text
s/^ \(..*\)$/T\1T/
t copy

# delete cuurent line and end
d
b end

# add the cuurent line to the hold space
:copy
H
b end

:default_size
s/\(.*\)END\(.*\)/\1S20S\2/
b comp_next

# draw!
:compute
G

/S[0-9][0-9]*/! b default_size
:comp_next

# reverse mode
/[^T]REVERSE/ b compute_rev

# print the text and then delete it from the pattern space
h
s/[^T]*T\(.*\)T[^T]*/\1/p
x
s/\([^T]*\)T.*T\([^T]*\)/\1\2/

# output the head
h
x
s/.*/    ╚⊙ ⊙╝/p
s/.*/  ╚═(███)═╝/
x

s/.*S\([0-9][0-9]*\)S.*/\1/
:compute_loop
s/^\([0-9]*\)0$/\1-/
s/^\([0-9]*\)1$/\10/
s/^\([0-9]*\)2$/\11/
s/^\([0-9]*\)3$/\12/
s/^\([0-9]*\)4$/\13/
s/^\([0-9]*\)5$/\14/
s/^\([0-9]*\)6$/\15/
s/^\([0-9]*\)7$/\16/
s/^\([0-9]*\)8$/\17/
s/^\([0-9]*\)9$/\18/
s/^2-$/19/
s/^1-$/9/
s/^9-$/89/
s/^8-$/79/
s/^7-$/69/
s/^6-$/59/
s/^5-$/49/
s/^4-$/39/
s/^3-$/29/
s/^-*//
/^$/ b end

x
p
/^  ╚═(███)═╝$/ b compute_step2
/^ ╚═(███)═╝$/ b compute_step3
/^╚═(███)═╝$/ b compute_step4
/^ ╚═(███)═╝ $/ b compute_step5
/^  ╚═(███)═╝ $/ b compute_step6
/^   ╚═(███)═╝ $/ b compute_step7
/^   ╚═(███)═╝$/ b compute_step8
:compute_next_step
x

b compute_loop

:compute_step2
s/^  ╚═(███)═╝$/ ╚═(███)═╝/
b compute_next_step

:compute_step3
s/^ ╚═(███)═╝$/╚═(███)═╝/
b compute_next_step

:compute_step4
s/^╚═(███)═╝$/ ╚═(███)═╝ /
b compute_next_step

:compute_step5
s/^ ╚═(███)═╝ $/  ╚═(███)═╝ /
b compute_next_step

:compute_step6
s/^  ╚═(███)═╝ $/   ╚═(███)═╝ /
b compute_next_step

:compute_step7
s/^   ╚═(███)═╝ $/   ╚═(███)═╝/
b compute_next_step

:compute_step8
s/^   ╚═(███)═╝$/  ╚═(███)═╝/
b compute_next_step

# reverse draw!
:compute_rev
h
x
s/.*/  ╔═(███)═╗/
x

:compute_rev_loop
s/\(.*S[0-9]*\)0\(S.*\)/\1-\2/
s/\(.*S[0-9]*\)1\(S.*\)/\10\2/
s/\(.*S[0-9]*\)2\(S.*\)/\11\2/
s/\(.*S[0-9]*\)3\(S.*\)/\12\2/
s/\(.*S[0-9]*\)4\(S.*\)/\13\2/
s/\(.*S[0-9]*\)5\(S.*\)/\14\2/
s/\(.*S[0-9]*\)6\(S.*\)/\15\2/
s/\(.*S[0-9]*\)7\(S.*\)/\16\2/
s/\(.*S[0-9]*\)8\(S.*\)/\17\2/
s/\(.*S[0-9]*\)9\(S.*\)/\18\2/
s/\(.*S\)2-\(S.*\)/\119\2/
s/\(.*S\)1-\(S.*\)/\19\2/
s/\(.*S\)9-\(S.*\)/\189\2/
s/\(.*S\)8-\(S.*\)/\179\2/
s/\(.*S\)7-\(S.*\)/\169\2/
s/\(.*S\)6-\(S.*\)/\159\2/
s/\(.*S\)5-\(S.*\)/\149\2/
s/\(.*S\)4-\(S.*\)/\139\2/
s/\(.*S\)3-\(S.*\)/\129\2/
/S-S/ b compute_rev_end

x
p
/^  ╔═(███)═╗$/ b compute_rev_step2
/^ ╔═(███)═╗$/ b compute_rev_step3
/^╔═(███)═╗$/ b compute_rev_step4
/^ ╔═(███)═╗ $/ b compute_rev_step5
/^  ╔═(███)═╗ $/ b compute_rev_step6
/^   ╔═(███)═╗ $/ b compute_rev_step7
/^   ╔═(███)═╗$/ b compute_rev_step8
:compute_rev_next_step
x

b compute_rev_loop

:compute_rev_step2
s/^  ╔═(███)═╗$/ ╔═(███)═╗/
b compute_rev_next_step

:compute_rev_step3
s/^ ╔═(███)═╗$/╔═(███)═╗/
b compute_rev_next_step

:compute_rev_step4
s/^╔═(███)═╗$/ ╔═(███)═╗ /
b compute_rev_next_step

:compute_rev_step5
s/^ ╔═(███)═╗ $/  ╔═(███)═╗ /
b compute_rev_next_step

:compute_rev_step6
s/^  ╔═(███)═╗ $/   ╔═(███)═╗ /
b compute_rev_next_step

:compute_rev_step7
s/^   ╔═(███)═╗ $/   ╔═(███)═╗/
b compute_rev_next_step

:compute_rev_step8
s/^   ╔═(███)═╗$/  ╔═(███)═╗/
b compute_rev_next_step

:compute_rev_end

x
/^╔═(███)═╗$/ b compute_rev_head1
/^ ╔═(███)═╗$/ b compute_rev_head2
/^ ╔═(███)═╗ $/ b compute_rev_head0
/^  ╔═(███)═╗$/ b compute_rev_head3
/^  ╔═(███)═╗ $/ b compute_rev_head1
/^   ╔═(███)═╗$/ b compute_rev_head3
/^   ╔═(███)═╗ $/ b compute_rev_head2

:compute_rev_head0
x
h
x
s/.*/  ╔⊙ ⊙╗/p
x
b compute_rev_text

:compute_rev_head1
x
h
x
s/.*/   ╔⊙ ⊙╗/p
x
b compute_rev_text

:compute_rev_head2
x
h
x
s/.*/    ╔⊙ ⊙╗/p
x
b compute_rev_text

:compute_rev_head3
x
h
x
s/.*/     ╔⊙ ⊙╗/p
x
b compute_rev_text

:compute_rev_text
# print the text and then delete it from the pattern space
h
s/[^T]*T\(.*\)T[^T]*/\1/p
x
s/\([^T]*\)T.*T\([^T]*\)/\1\2/

:end
}' << EOF
 $1
 $2
 $3
END
EOF
